
description = 'dynmap'

eclipse {
    project {
        name = "Dynmap(Spigot)"
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.codemc.org/repository/maven-releases/'
    }
    maven {
        name = "devmart-other"
        url = uri("https://nexuslite.gcnt.net/repos/other/")
    }
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

dependencies {
    implementation('org.bukkit:bukkit:1.10.2-R0.1-SNAPSHOT') { transitive = false }
    "implementation"("com.tcoded:FoliaLib:0.2.3")
    compileOnly('com.nijikokun.bukkit:Permissions:3.1.6')  { transitive = false }
    compileOnly('me.lucko.luckperms:luckperms-api:4.3')  { transitive = false }
    compileOnly('net.luckperms:api:5.0')  { transitive = false }
    compileOnly('com.github.MilkBowl:VaultAPI:1.7') { transitive = false }
    compileOnly('net.skinsrestorer:skinsrestorer-api:14.2.+')  { transitive = false }
    implementation(project(":dynmap-api"))  { transitive = false }
    implementation(project(path: ":DynmapCore", configuration: "shadow"))  { transitive = false }
    compileOnly('ru.tehkode:PermissionsEx:1.19.1')  { transitive = false }
    compileOnly('de.bananaco:bPermissions:2.9.1')  { transitive = false }
    compileOnly('com.platymuus.bukkit.permissions:PermissionsBukkit:1.6')  { transitive = false }
    compileOnly('org.anjocaido:EssentialsGroupManager:2.10.1')  { transitive = false }
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.2'
    compileOnly('com.googlecode.json-simple:json-simple:1.1.1') { transitive = false }
    compileOnly('com.google.code.gson:gson:2.8.9') { transitive = false }
    implementation(project(':bukkit-helper')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-120')) {
    	transitive = false
    }
    implementation(project(':bukkit-helper-120-2')) {
    	transitive = false
    }
}

processResources {
    // replace stuff in mcmod.info, nothing else
    filesMatching('plugin.yml') {
        // replace version and mcversion
        expand(
                buildnumber: project.parent.ext.globals.buildNumber,
                version: project.version
        )
    }
}

jar {
    classifier = 'unshaded'
}

shadowJar {
    dependencies {
        include(dependency('org.bstats::'))
        include(dependency(':dynmap-api'))
        include(dependency(":DynmapCore"))
        include(dependency(':bukkit-helper'))
        include(dependency(':bukkit-helper-120'))
        include(dependency(':bukkit-helper-120-2'))
    }
    relocate('org.bstats', 'org.dynmap.bstats')
    destinationDir = file '../target'
    archiveName = "Dynmap-${parent.version}-spigot.jar"
    classifier = ''
    relocate ("com.tcoded.folialib", "com.sk89q.worldguard.bukkit.folialib") {
        include(dependency("com.tcoded:FoliaLib"))
    }
}
shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

artifacts {
    archives shadowJar
}
